MODULE_NAME='mExtronMGP'(DEV vdvControl,DEV dvRS232)
INCLUDE 'CustomFunctions'

DEFINE_CONSTANT
LONG TLID_COMMS = 1
LONG TLID_POLL	 = 2
DEFINE_VARIABLE
INTEGER DEBUG = 0
LONG TLT_COMMS[] = {60000} 
LONG TLT_POLL[]  = {15000}

DEFINE_FUNCTION fnSendCommand(CHAR pToSend[255]){
	SEND_STRING dvRS232, pToSend
	fnInitPoll()
}

DEFINE_FUNCTION fnInitPoll(){
	IF(TIMELINE_ACTIVE(TLID_POLL)){ TIMELINE_KILL(TLID_POLL) }
	TIMELINE_CREATE(TLID_POLL,TLT_POLL,LENGTH_ARRAY(TLT_POLL),TIMELINE_ABSOLUTE,TIMELINE_REPEAT)
}
DEFINE_EVENT TIMELINE_EVENT[TLID_POLL]{
	SEND_STRING dvRS232, 'Q'
}

DEFINE_EVENT DATA_EVENT[dvRS232]{
	ONLINE:{
		SEND_COMMAND dvRS232, 'SET MODE DATA'
		SEND_COMMAND dvRS232, 'SET BAUD 9600 N 8 1 485 DISABLE'
		fnSendCommand('Q')
	}
	STRING:{
		IF(TIMELINE_ACTIVE(TLID_COMMS)){TIMELINE_KILL(TLID_COMMS)}
		TIMELINE_CREATE(TLID_COMMS,TLT_COMMS,LENGTH_ARRAY(TLT_COMMS),TIMELINE_ABSOLUTE,TIMELINE_ONCE)
	}
}


DATA_EVENT[vdvControl]{
	COMMAND:{
		SWITCH(fnStripCharsRight(REMOVE_STRING(DATA.TEXT,'-',1),1)){
			CASE 'DEBUG': DEBUG = (ATOI(DATA.TEXT) || DATA.TEXT == 'TRUE')
			CASE 'MATRIX':fnSendCommand("DATA.TEXT,'!'")
			CASE 'PRESET1':fnSendCommand("'1*',DATA.TEXT,'.'")	// Without Input
			CASE 'PRESET2':fnSendCommand("'2*',DATA.TEXT,'.'")	// With Input
		}
	}
}

DEFINE_PROGRAM{
	[vdvControl,251] = (TIMELINE_ACTIVE(TLID_COMMS))
	[vdvControl,252] = (TIMELINE_ACTIVE(TLID_COMMS))
}